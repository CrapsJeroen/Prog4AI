:- use_module(library(chr)).
:- use_module(library(lists)).
:- op(700,xfx,'in').
:- [sudex_toledo].
:- chr_constraint solve/1, import/1, import/2, import_row/3, option/3, known/3, dom/1, search/0, try_row/1, show/0, show/2, clean/0.

%%% MAIN %%%
	
/*
*	Solve the puzzle with the given Name.
*/	
solve(Name) <=>
  puzzles(Puzzle,Name),
  length(Puzzle,N2),
  dom(N2),
  import(Puzzle),
        show,
  search,
  !,
  show,
	clean.
	
/*
*	Solve all puzzles and pause inbetween.
*/
solve_all:-
    puzzles(_,Name),
    write(Name), nl,
    time(solve(Name)).

/*
*	Solve all puzzles without pausing.
*/
solve_all_auto:-
	time((solve_all,fail; true)).
	
/*
* 	Show the Sudoku
*/	
show <=> nl, show(1,1).
dom(N) \ show(Row,Col) <=> Col > N | NRow is Row + 1, nl, show(NRow,1).
dom(N) \ show(Row,_) <=> Row > N | true.
known(Row,Col,Val) \ show(Row,Col) <=> format('~10r ',Val), Col1 is Col+1, show(Row,Col1).
show(Row,Col) <=> format('. '), Col1 is Col+1, show(Row,Col1).

/*
* 	Clean up CHR constraints
*/
clean \ known(_,_,_) <=> true.
clean, dom(_) <=> true.

/*
* 	Import the Sudoku puzzle
*/
import(Puzzle) <=> import(Puzzle,1).
import([],_) <=> true.
import([CRow|Rows],Y) <=>
    import_row(CRow,Y,1), Y1 is Y + 1 ,import(Rows,Y1).
import_row([],_,_) <=> true.
import_row([CVal|Vals],Y,X) <=>
    nonvar(CVal) | known(Y,X,CVal), X1 is X + 1, import_row(Vals,Y,X1).
import_row([CVal|Vals],Y,X) <=>
    var(CVal) | upto(9,L), option(Y,X,L), X1 is X + 1, import_row(Vals,Y,X1).

	
	
%%% CONSTRAINTS %%%

	
/*
* 	Fail if multiple fields on the same row only have the same option left or
* 	if one field has no options left.
*/	
option(X,_,[V]), option(X,_,[V]) <=> fail.
option(_,_,[]) <=> fail.

/*
* 	Remove the option of value V if there is a known field 
* 	with V in the same row
*/
known(X,_,V) \ option(X,Y,L) <=> member(V,L) | delete(L,V,NL), option(X,Y,NL).

/*
* 	Remove the option of value V if there is a known field 
* 	with V in the same column
*/
known(_,Y,V) \ option(X,Y,L) <=> member(V,L) | delete(L,V,NL), option(X,Y,NL).

/*
* 	Remove the option of value V if there is a known field 
* 	with V in the same block
*/
known(X,Y,V), dom(N2) \ option(X1,Y1,L) <=> member(V,L),
        N is integer(sqrt(N2)),
        XB is (X-1)//N, YB is (Y-1)//N,
        XB is (X1-1)//N, YB is (Y1-1)//N
         | delete(L,V,NL), option(X1,Y1,NL).

/*
* 	If there is only one option left for a field, its value is known
*/		 
option(X,Y,[X1]) <=> known(X,Y,X1).


%%% SEARCH %%%

/*
*	Search by filling in values row by row
*/
option(X,_,_) \ search  <=> try_row(X), search.
search <=> true.

/*
*	Fill in values for the given row.
*/
try_row(X) \ option(X,Y,List) <=> try_value(X,Y,List).
try_row(_) <=> true.


/*
*	Try to fill in a value from the List in field with coordinates X and Y
*/
try_value(X,Y,List) :-
  member(V,List),
  (
    known(X,Y,V)
  ;
    delete(List,V,Result),
    option(X,Y,Result)
  ).
  
  
%%% HELPER PREDICATES %%%
 
/*
* 	Generate a list going from 1 to N.
*/ 
upto(0,[]).
upto(N,[N|L]) :- N>0, N1 is N-1, upto(N1,L).
